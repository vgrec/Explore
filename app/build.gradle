apply plugin: 'com.android.application'

// Read the API keys. API keys should be defined in a properties file called keys.properties
// located in the project root directory.
Properties properties = new Properties()
properties.load(project.rootProject.file('keys.properties').newDataInputStream())
def placesApiKey = properties.getProperty('places_api_key')
def youtubeApiKey = properties.getProperty('youtube_api_key')
def flickrApiKey = properties.getProperty('flickr_api_key')


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.vgrec.explore"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 7
        versionName "1.1"

        buildConfigField("String", "PLACES_API_KEY", "\"" + placesApiKey + "\"")
        buildConfigField("String", "YOUTUBE_API_KEY", "\"" + youtubeApiKey + "\"")
        buildConfigField("String", "FLICKR_API_KEY", "\"" + flickrApiKey + "\"")

        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-maps:8.4.0'

    // Support libs
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    implementation 'com.mcxiaoke.volley:library-aar:1.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.4.1.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.1.3'
    implementation 'com.jakewharton:butterknife:6.0.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'de.greenrobot:eventbus:2.4.0'
    implementation 'joda-time:joda-time:2.3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}


android {
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}
